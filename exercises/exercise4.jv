transform ConvertCelsiusToFahrenheit {
  from temperatureInCelsius oftype decimal;
  to temperatureInFahrenheit oftype decimal;

  temperatureInFahrenheit: (temperatureInCelsius * 9/5) + 32;
}

constraint MonthRangeConstraint oftype RangeConstraint {
    lowerBound: 1;
    upperBound: 12;
}

constraint TemperatureAbsoluteZero oftype RangeConstraint {
  lowerBound: -273.150;
  lowerBoundInclusive: true;
    
}

valuetype Month oftype integer {
    constraints: [MonthRangeConstraint];
}

constraint TrueFalseGermanConstraint oftype AllowlistConstraint {
    allowlist: ["Ja", "Nein"];
}

valuetype GermanBoolean oftype text {
    constraints: [TrueFalseGermanConstraint];
}

pipeline SensorDataPipeline {
    block DownloadData oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block ExtractZipFile oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block SelectDataFile oftype FilePicker {
        path: "/data.csv";
    }

    block ReadTextFile oftype TextFileInterpreter {}
    block ParseCsv oftype CSVInterpreter { delimiter: ';';}

    block ChooseColumns oftype CellRangeSelector {
        select: range A1:K*;
    }

    block DiscardColumns oftype ColumnDeleter {
        delete: [column F, column G, column H, column I];
    }

    block DataTableSetup oftype TableInterpreter {
        header: false;
        columns: [
            "Geraet" oftype integer,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype Month,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype GermanBoolean,
        ];
    }

    block ConvertAmbientTemp oftype TableTransformer {
        inputColumns: ["Temperatur"];
        outputColumn: "Temperatur";
        use: ConvertCelsiusToFahrenheit;
    }

    block ConvertBatteryTemp oftype TableTransformer {
        inputColumns: ["Batterietemperatur"];
        outputColumn: "Batterietemperatur";
        use: ConvertCelsiusToFahrenheit;
    }

    block LoadDataToDatabase oftype SQLiteLoader {
        table: "temperatures"; 
        file: "./temperatures.sqlite";
    }


    DownloadData
        -> ExtractZipFile
        -> SelectDataFile
        -> ReadTextFile
        -> ParseCsv
        -> ChooseColumns
        -> DiscardColumns
        -> DataTableSetup
        -> ConvertAmbientTemp
        -> ConvertBatteryTemp
        -> LoadDataToDatabase;
}

